 LST OFF
 XC OFF ; 6502 MODE
********************************
* LOGIC ANALYZER DISPLAY       *
********************************
*
 DSK LOGIC
 ORG $8000
 TYP $06
 JMP MAIN
*-------------------------------
* ZERO PAGES
*-------------------------------
*
PRVBIT EQU $80
CURSMPL EQU $81
TRANS EQU $82
SMPLPTR EQU $83
BUFFPTR EQU $84
CHANSAVE EQU $86
LINENUM EQU $87
TEMP EQU $90
CURCOL EQU $92
CURBIT EQU $93
CURMASK EQU $94
ACOL EQU $95
ABIT EQU $96
BCOL EQU $97
BBIT EQU $98
TXTPTR EQU $FA
SMPLBLK EQU $FB
SMPRATE EQU $FC
*
*-------------------------------
* CONSTANTS
*-------------------------------
*
NUMCOLS EQU 38 ; COLS PER ROW
BASELINE EQU 0 ; STARTING LINE FOR CHAN 0
*
*-------------------------------
* UTILITIES                    *
*-------------------------------
*
 PUT INCLUDE ; INCLUDE DRAW UTILS
BELL EQU $FF3A
*
*-------------------------------
* MACROS                       *
*-------------------------------
*-------------------------------
* FILLBUF - READ SAMPLES FROM FPGA
*-------------------------------
* IN:  Y = CHANNEL NUMBER (0-7)
* OUT: TMPBUF FILLED WITH 38 SAMPLES
* CLOBBERS: A, X
* PRESERVES: Y
*-------------------------------
* FPGA REGISTERS:
*   $C0C0 = CHANNEL SELECT
*   $C0C1 = SAMPLE ADDRESS
*   $C0C2 = DATA OUT
*   $C0C3 = COMMAND (2=READ)
*   $C0C4 = STATUS (BIT0=BUSY)
*-------------------------------
FILLBUF
 STY $C0C0           ; SELECT CHANNEL
 LDX #$00            ; SAMPLE INDEX
:RDLOOP
 STX $C0C1           ; SET SAMPLE ADDR
 LDA #$02
 STA $C0C3           ; SEND READ CMD
:WAIT
 LDA $C0C4           ; CHECK STATUS
 LSR                 ; BIT0 -> CARRY
 BCS :WAIT           ; LOOP IF BUSY
 LDA $C0C2           ; READ DATA
 STA TMPBUF,X        ; STORE IN BUFFER
 INX
 CPX #$26            ; 38 SAMPLES
 BNE :RDLOOP
 LDA #$02
 STA $C0C4           ; SIGNAL READY
 RTS


GETSMPL MAC
 LDA #<]1
 STA SMPLPTR
 LDA #>]1
 STA SMPLPTR+1
 JSR CALCHAN
 <<<
*
*-------------------------------
*SETPRINT MACRO                *
*-------------------------------
*
SETPRINT MAC
 LDA #<]1 ; LINE
 STA TXTPTR
 LDA #>]1
 STA TXTPTR+1

 LDA #]2
 JSR PRINTLN
 <<<
*
*-------------------------------
* MAIN - ENTRY POINT
*-------------------------------
MAIN
 JSR HGR              ; CLEAR SCREEN + HIRES MODE
*
* INIT CURSOR MARKERS (A/B) AS UNSET
 LDA #$FF
 STA ACOL
 STA BCOL
*
*-------------------------------
* INIT FPGA / WAIT FOR READY
*-------------------------------
 LDA #$0C
 STA SMPRATE          ; STORE IN ZP
 STA $C0C5            ; CONFIG: SET SAMPLE RATE
 LDA #$10
 STA $C0C3            ; CMD: START CAPTURE
:WRDY
 LDA $C0C4            ; CHECK STATUS
 AND #$02             ; BIT1 = DATA READY
 BEQ :WRDY            ; WAIT UNTIL READY
*
* CLEAR TEXT AREA + SHOW STATUS
 JSR CLRTXT
 JSR SHOWRATE
 >>> SETPRINT.MSG;0
*
*-------------------------------
* DRAW ALL 8 CHANNELS
*-------------------------------
* LOOP Y = 0..7 (CHANNEL INDEX)
*-------------------------------
 LDY #$00
:CHLOOP
 STY CHANSAVE         ; SAVE CHANNEL #
 JSR FILLBUF          ; READ SAMPLES FROM FPGA
* PROCESS SAMPLES -> ROW1/TRANS/ROW8 BUFS
 LDA #<TMPBUF
 STA SMPLPTR
 LDA #>TMPBUF
 STA SMPLPTR+1
 JSR CALCHAN
* GET STARTING HIRES LINE FOR CHANNEL
 LDY CHANSAVE
 LDA CHANLINES,Y
 STA LINENUM
* DRAW ROW1 (TOP EDGE)
 LDX LINENUM
 JSR DCROW1
* DRAW 5 TRANSITION LINES (MIDDLE)
 LDA #$05
 STA TEMP
:TRLOOP
 INC LINENUM
 LDX LINENUM
 JSR DCTRANS
 DEC TEMP
 BNE :TRLOOP
* DRAW ROW8 (BOTTOM EDGE)
 INC LINENUM
 LDX LINENUM
 JSR DCROW8
* NEXT CHANNEL
 LDY CHANSAVE
 INY
 CPY #$08
 BNE :CHLOOP
*
*-------------------------------
* DRAW TOP BORDER
*-------------------------------
*
 LDA #$7F
 LDX #$00
 JSR DRAWBRD
 LDX #$01
 JSR DRAWBRD ; A SHOULD STILL HAVE #$7F

 LDA #$01
 LDX #$02
 JSR DRAWBRD
 LDX #$03
 JSR DRAWBRD ; A SHOULD STILL HAVE #$01
*
*-------------------------------
* DRAW SIDE BORDERS
*-------------------------------
*
 LDX #$02
BORDLR
 LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDA #$03
 LDY #$00
 STA (LOW),Y
 LDA #$60
 LDY #$27
 STA (LOW),Y
 INX
 CPX #$9F
 BNE BORDLR
*
*-------------------------------
* DRAW BOTTOM BORDER
*-------------------------------
*
 LDA #$7F
 LDX #$9F
 JSR DRAWBRD
 LDX #$9E
 JSR DRAWBRD ; A SHOULD STILL HAVE #$7F

 LDA #$01
 LDX #$9D
 JSR DRAWBRD
 LDX #$9C
 JSR DRAWBRD ; A SHOULD STILL HAVE #$01
*
*-------------------------------
* TODO FIX CORNERS
*-------------------------------
*
*-------------------------------
* DRAW CURSOR
*-------------------------------
 LDA #$14 ; COL 20
 STA CURCOL

 JSR CALCSAMP
 JSR CONVTIME
; CURRENT TIME
 >>> SETPRINT.MSG2;1
 >>> SETPRINT.MSGA;2
 >>> SETPRINT.MSGB;3

 LDA #$01 ; BIT 3
 STA CURBIT
 LDX CURBIT
 LDA BITMASKS,X
 STA CURMASK
 JSR DRAWCUR
*
 JMP MAINLOOP
*
********************************
*
*------------------------
* SUBROUTINES
*-------------------------------
*
QUIT STA $C051
 STA $C054
 RTS
*
CLRTXT LDA #$A0
 LDX #39
:LOOP STA $650,X
 STA $6D0,X
 STA $750,X
 STA $7D0,X
 DEX
 BPL :LOOP
 RTS
*
PRINTLN TAX
 LDA TXTLO,X
 STA STAUX+1
 STA STMAIN+1
 LDA TXTHI,X
 STA STAUX+2
 STA STMAIN+2

 LDY #$00
 LDX #$00
PRLOOP LDA (TXTPTR),Y
 BEQ PRDONE
 ORA #$80
 STA $C055
STAUX STA $FFFF,X
 INY

 LDA (TXTPTR),Y
 BEQ PRDONE2
 ORA #$80
 STA $C054
STMAIN STA $FFFF,X
 INY
 INXO
 CPX #40
 BCC PRLOOP

PRDONE2 STA $C054
PRDONE STA $C054
 RTS
*
*-------------------------------
* MAIN LOOP
*-------------------------------
*
MAINLOOP LDA $C000
 BPL MAINLOOP ; NO KEYBOARD
 STA $C010 ; STROBE

 CMP #$9B ; ESC
 BEQ QUIT

 CMP #$88 ; LEFT
 BEQ GOLEFT

 CMP #$95 ; RIGHT
 BEQ :GORIT

 CMP #$C1 ; 'A'
 BEQ :GOA

 CMP #$C2 ; 'B'
 BEQ :GOB

 CMP #$C3 ; 'C'
 BEQ :GOC

 CMP #$D3 ; 'S'
 BEQ :GOS

 CMP #$D2 ; 'R'
 BEQ :GOR

 JMP MAINLOOP
:GORIT JMP GORIGHT
:GOA JMP DROPA
:GOB JMP DROPB
:GOC JMP CLEARAB
:GOS JMP SETRATE
:GOR JMP REGEN
*
*-------------------------------
* LEFT CURSOR
*-------------------------------
*
MVLEFT LDA CURBIT
 BNE LEFTBIT
 LDA CURCOL
 CMP #$01 ; EDGE?
 BEQ :BLOCK ; BEEP + DONT MOVE
 DEC CURCOL
 LDA #$06 ; BIT 6
 STA CURBIT
 JMP UPDMASK
:BLOCK JMP BLOCKED

LEFTBIT LDA $C061 ; OPEN APPLE?
 BMI :SKIP10
 DEC CURBIT ; SINGLE
 JMP UPDMASK
:SKIP10 TXA ; SAVE X
 PHA
 LDX #10
:LOOP LDA CURBIT
 BEQ :DONE ; HIT BIT 0, STOP
 DEC CURBIT
 DEX
 BNE :LOOP
:DONE PLA ; RESTORE X
 TAX
 JMP UPDMASK

GOLEFT JSR DRAWCUR ; ERASE
 JSR MVLEFT
 JSR DRAWCUR ; DRAW
 JSR CALCSAMP
 JSR CONVTIME
; TIME
 >>> SETPRINT.MSG2;1

 JMP MAINLOOP
*
*-------------------------------
* RIGHT CURSOR
*-------------------------------
*
MVRIGHT LDA CURBIT
 CMP #$06 ; BIT 6?
 BNE RIGHTBIT
 LDA CURCOL
 CMP #$26 ; EDGE?
 BEQ :BLOCK
 INC CURCOL
 LDA #$00
 STA CURBIT
 JMP UPDMASK
:BLOCK JMP BLOCKED

RIGHTBIT LDA $C061 ; OPEN APPLE?
 BMI :SKIP10
 INC CURBIT ; SINGLE
 JMP UPDMASK
:SKIP10 TXA ; SAVE X
 PHA
 LDX #10
:LOOP LDA CURBIT
 CMP #$06 ; HIT BIT 6?
 BEQ :DONE ; STOP
 INC CURBIT
 DEX
 BNE :LOOP
:DONE PLA ; RESTORE X
 TAX
 JMP UPDMASK

GORIGHT JSR DRAWCUR ; ERASE
 JSR MVRIGHT
 JSR DRAWCUR ; DRAW
 JSR CALCSAMP
 JSR CONVTIME
; TIME
 >>> SETPRINT.MSG2;1
 JMP MAINLOOP
*
*-------------------------------
* DROP MARKER A
*-------------------------------
*
DROPA LDA ACOL
 CMP #$FF ; NOT SET?
 BEQ :SETNEW
* ERASE OLD MARKER
 LDA CURCOL
 PHA
 LDA CURMASK
 PHA
 LDA ACOL
 STA CURCOL
 LDX ABIT
 LDA BITMASKS,X
 STA CURMASK
 JSR DRAWCUR
* RESTORE
 PLA
 STA CURMASK
 PLA
 STA CURCOL
:SETNEW LDA CURCOL
 STA ACOL
 LDA CURBIT
 STA ABIT
 JSR DRAWCUR
* COPY TIME + PRINT
 LDX #$07
:CPYA LDA TIMESTR,X
 STA TIMEASTR,X
 DEX
 BPL :CPYA
 >>> SETPRINT.MSGA;2
 JMP MAINLOOP
*
*-------------------------------
* DROP MARKER B
*-------------------------------
*
DROPB LDA BCOL
 CMP #$FF ; NOT SET?
 BEQ :SETNEW
* ERASE OLD MARKER
 LDA CURCOL
 PHA
 LDA CURMASK
 PHA
 LDA BCOL
 STA CURCOL
 LDX BBIT
 LDA BITMASKS,X
 STA CURMASK
 JSR DRAWCUR
* RESTORE
 PLA
 STA CURMASK
 PLA
 STA CURCOL
:SETNEW LDA CURCOL
 STA BCOL
 LDA CURBIT
 STA BBIT
 JSR DRAWCUR
* COPY TIME + PRINT
 LDX #$07
:CPYB LDA TIMESTR,X
 STA TIMEBSTR,X
 DEX
 BPL :CPYB
 >>> SETPRINT.MSGB;3
 JMP MAINLOOP
*
*-------------------------------
* CLEAR MARKERS A + B
*-------------------------------
*
CLEARAB JSR ERASEMRK
 JSR CLRSTATE
 JMP MAINLOOP
*
*-------------------------------
* ERASE MARKER GRAPHICS (SUBROUTINE)
*-------------------------------
*
ERASEMRK LDA CURCOL
 PHA
 LDA CURMASK
 PHA
* ERASE A IF SET
 LDA ACOL
 CMP #$FF
 BEQ :CLRB
 STA CURCOL
 LDX ABIT
 LDA BITMASKS,X
 STA CURMASK
 JSR DRAWCUR
* ERASE B IF SET
:CLRB LDA BCOL
 CMP #$FF
 BEQ :DONE
 STA CURCOL
 LDX BBIT
 LDA BITMASKS,X
 STA CURMASK
 JSR DRAWCUR
* RESTORE
:DONE PLA
 STA CURMASK
 PLA
 STA CURCOL
 RTS
*
*-------------------------------
* CLEAR MARKER STATE (SUBROUTINE)
*-------------------------------
*
CLRSTATE LDA #$FF
 STA ACOL
 STA BCOL
* RESET TIME STRINGS
 LDA #$2D ; '-'
 LDX #$07
:CLRT STA TIMEASTR,X
 STA TIMEBSTR,X
 DEX
 BPL :CLRT
 >>> SETPRINT.MSGA;2
 >>> SETPRINT.MSGB;3
 RTS
*
*-------------------------------
* SET SAMPLE RATE
*-------------------------------
*
SETRATE JSR CLRTXT
 >>> SETPRINT.MSGRATE;0
* GET FIRST HEX DIGIT
:WAIT1 LDA $C000
 BPL :WAIT1
 STA $C010
 CMP #$8D ; ENTER = CANCEL
 BEQ :DONE
 JSR HEXVAL
 BCS :WAIT1 ; NOT HEX, TRY AGAIN
 ASL
 ASL
 ASL
 ASL
 STA HEXBYTE ; STORE HIGH NIBBLE
* ECHO FIRST CHAR (POS 14 = AUX, OFFSET 7)
 LDA HEXBYTE
 LSR
 LSR
 LSR
 LSR ; GET HIGH NIBBLE BACK
 JSR HEXASC
 STA $C055 ; AUX
 STA $657 ; LINE 0, OFFSET 7
 STA $C054 ; MAIN
* GET SECOND HEX DIGIT
:WAIT2 LDA $C000
 BPL :WAIT2
 STA $C010
 CMP #$8D ; ENTER = USE JUST HIGH
 BEQ :APPLY
 JSR HEXVAL
 BCS :WAIT2 ; NOT HEX, TRY AGAIN
 ORA HEXBYTE
 STA HEXBYTE
* ECHO SECOND CHAR (POS 15 = MAIN, OFFSET 7)
 LDA HEXBYTE
 AND #$0F ; GET LOW NIBBLE
 JSR HEXASC
 STA $657 ; LINE 0, OFFSET 7 (MAIN)
* WAIT FOR ENTER
:WAIT3 LDA $C000
 BPL :WAIT3
 STA $C010
 CMP #$8D ; ENTER
 BNE :WAIT3
* APPLY NEW RATE
:APPLY LDA HEXBYTE
 STA SMPRATE
 STA $C0C5 ; SET FPGA RATE
* RESTART CAPTURE
 LDA #$10
 STA $C0C3 ; CMD: START CAPTURE
:WRDY2 LDA $C0C4
 AND #$02
 BEQ :WRDY2
:DONE JSR CLRTXT
 JSR SHOWRATE
 >>> SETPRINT.MSG;0
 >>> SETPRINT.MSG2;1
 >>> SETPRINT.MSGA;2
 >>> SETPRINT.MSGB;3
 JMP MAINLOOP
*
HEXBYTE DS 1
*
*-------------------------------
* CONVERT ASCII HEX TO VALUE
* IN: A = ASCII CHAR (HI BIT SET)
* OUT: A = 0-15, C=0 IF VALID
*      C=1 IF INVALID
*-------------------------------
*
HEXVAL AND #$7F ; STRIP HI BIT
 CMP #$30 ; < '0'?
 BCC :BAD
 CMP #$3A ; <= '9'?
 BCC :DIGIT
 CMP #$41 ; < 'A'?
 BCC :BAD
 CMP #$47 ; <= 'F'?
 BCS :BAD
 SEC
 SBC #$37 ; 'A'-10
 CLC
 RTS
:DIGIT SEC
 SBC #$30 ; '0'
 CLC
 RTS
:BAD SEC
 RTS
*
*-------------------------------
* NIBBLE TO ASCII HEX
* IN: A = 0-15
* OUT: A = ASCII CHAR (HI BIT SET)
*-------------------------------
*
HEXASC CMP #$0A
 BCC :ISDIG
 CLC
 ADC #$37 ; 'A'-10
 ORA #$80
 RTS
:ISDIG CLC
 ADC #$30 ; '0'
 ORA #$80
 RTS
*
*-------------------------------
* REGENERATE SIGNALS
*-------------------------------
*
REGEN
* CLEAR MARKERS
 JSR ERASEMRK
 JSR CLRSTATE
* ERASE MAIN CURSOR
 JSR DRAWCUR
* RESTART CAPTURE WITH CURRENT RATE
 LDA SMPRATE
 STA $C0C5
 LDA #$10
 STA $C0C3 ; CMD: START CAPTURE
:WRDY LDA $C0C4
 AND #$02
 BEQ :WRDY
* REDRAW ALL 8 CHANNELS
 LDY #$00
:CHLOOP STY CHANSAVE
 JSR FILLBUF
 LDA #<TMPBUF
 STA SMPLPTR
 LDA #>TMPBUF
 STA SMPLPTR+1
 JSR CALCHAN
 LDY CHANSAVE
 LDA CHANLINES,Y
 STA LINENUM
 LDX LINENUM
 JSR DCROW1
 LDA #$05
 STA TEMP
:TRLOOP INC LINENUM
 LDX LINENUM
 JSR DCTRANS
 DEC TEMP
 BNE :TRLOOP
 INC LINENUM
 LDX LINENUM
 JSR DCROW8
 LDY CHANSAVE
 INY
 CPY #$08
 BNE :CHLOOP
* RESTORE LEFT BORDER
 LDX #$02
:BRDLP LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDA #$03
 LDY #$00
 STA (LOW),Y
 INX
 CPX #$9F
 BNE :BRDLP
* REDRAW MAIN CURSOR
 JSR DRAWCUR
 JMP MAINLOOP
*
*-------------------------------
* SHOW RATE IN DECIMAL
*-------------------------------
*
SHOWRATE LDA SMPRATE
 STA RATEVAL
 LDA #$00
 STA RATEVAL+1
* CONVERT TO 3 DECIMAL DIGITS
 LDX #$02 ; 3 DIGITS
:LOOP LDA #$00
 STA REMAIN
 LDY #16 ; 16 BITS
:DIV ASL RATEVAL
 ROL RATEVAL+1
 ROL REMAIN
 LDA REMAIN
 CMP #10
 BCC :NOSUB
 SBC #10
 STA REMAIN
 INC RATEVAL
:NOSUB DEY
 BNE :DIV
 LDA REMAIN
 CLC
 ADC #$30 ; ASCII '0'
 STA RATESTR,X
 DEX
 BPL :LOOP
 RTS
*
RATEVAL DS 2
*
*-------------------------------
* CURSOR UPDATE
*-------------------------------
*
UPDMASK LDX CURBIT
 LDA BITMASKS,X
 STA CURMASK
 RTS

BLOCKED JSR BELL
 RTS

*
DRAWCUR LDX #$00
DCLOOP LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY CURCOL
 LDA (LOW),Y
 EOR CURMASK
 STA (LOW),Y
 INX
 CPX #$A0
 BNE DCLOOP
 RTS
*
*-------------------------------
* CALC PRINT TIME              *
*-------------------------------
*
CALCSAMP LDA CURCOL
 SEC
 SBC #$01 ; 0 -37
 STA TEMP2 ; SAVE

 LDA #$00
 STA VALMID ; CLEAR HIGH BYTE
 LDA TEMP2
 ASL ; X 2
 ROL VALMID
 ASL  ; X 4
 ROL VALMID
 ASL  ; X 8
 ROL VALMID
 STA VALLO

 ; SUB (COL-1)
 LDA VALLO
 SEC
 SBC TEMP2
 STA VALLO
 LDA VALMID
 SBC #$00 ; BORROW
 STA VALMID

 ; ADD CURBIT
 LDA VALLO
 CLC
 ADC CURBIT
 STA VALLO
 LDA VALMID
 ADC #$00
 STA VALMID

 LDA #$00
 STA VALHI

 ; BUFFER OFFSET
 LDA VALLO
 CLC
 ADC BUFLO
 STA VALLO
 LDA VALMID
 ADC BUFMID
 STA VALMID
 LDA VALHI
 ADC BUFHI
 STA VALHI
 RTS
*
TEMP2 DS 1
*
CONVTIME LDX #$07 ; 8 DIGITS, START RIGHT
:LOOP LDA #$00
 STA REMAIN

 LDY #24 ; 24 BITS
:DIV ASL VALLO
 ROL VALMID
 ROL VALHI
 ROL REMAIN
 LDA REMAIN
 CMP #10
 BCC :NOSUB
 SBC #10
 STA REMAIN
 INC VALLO
:NOSUB DEY
 BNE :DIV

 LDA REMAIN
 CLC
 ADC #$B0 ; ASCII "0", HI BIT
 STA TIMESTR,X
 DEX
 BPL :LOOP
 RTS
*
*-------------------------------
* DRAW LOOPS                   *
* LDX BEFORE CALLING           *
*-------------------------------
*
DRAWBRD PHA  ;STASH THE VAL TO WRITE
 LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY #$00
 PLA  ; RESTORE THE VALUE TO WRITE
]DRAW STA (LOW),Y
 INY
 CPY #NUMCOLS+2 ; TO THE END
 BNE ]DRAW
 RTS
*
*-------------------------------
*DRAW CHANNEL - LOAD X BEFORE  *
*-------------------------------
*
DCROW1 LDA #<ROW1BUF
 STA BUFFPTR
 LDA #>ROW1BUF
 STA BUFFPTR+1
 JMP DRCH
*
DCTRANS LDA #<TRANSBUF
 STA BUFFPTR
 LDA #>TRANSBUF
 STA BUFFPTR+1
 JMP DRCH
*
DCROW8 LDA #<ROW8BUF
 STA BUFFPTR
 LDA #>ROW8BUF
 STA BUFFPTR+1
* FALL THROUGH TO DRCH
*
DRCH LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY #$00
]DRAW LDA (BUFFPTR),Y
 STA (LOW),Y
 INY
 CPY #NUMCOLS+1
 BNE ]DRAW
 RTS
*
*-------------------------------
* CALC LOOP                    *
*-------------------------------
*
CALCHAN LDA #$00
 STA PRVBIT ; INIT PREV TO 0
 LDY #$00 ; COL COUNT
CALCLOOP
 LDA (SMPLPTR),Y ; GET SAMPLE
 STA CURSMPL ; SAVE
;CALC TRANSITIONS
;TRANS = SAMPLE EOR ((SAMPLE ASL)
;            AND #$7E ORA PRVBIT)
 ASL ; SHIFT LEFT
 AND #$7E ; CLEAR BITS 0 & 7
 ORA PRVBIT ; BRING IN PREV BIT 6
 EOR CURSMPL ; XOR W/ ORIG = TRANS
 STA TRANS
 STA TRANSBUF,Y ; STORE TRANSITIONS
; CALC ROW1 = SAMPLE ORA TRANS
 LDA CURSMPL
 ORA TRANS
 STA ROW1BUF,Y
; CALC ROW8 = !SAMPLE ORA TRANS
 LDA CURSMPL
 EOR #$7F ;INVERT ONLY 7 BITS
 ORA TRANS
 STA ROW8BUF,Y
; SAVE BIT 6 FOR NEXT COL PRVBIT
 LDA CURSMPL
 AND #$40 ; GET BIT 6
 BEQ SAVEPREV ; IF 0, STORE 0
 LDA #$01 ; IF SET, STORE 1
SAVEPREV STA PRVBIT
 INY
 CPY #NUMCOLS+1
 BNE CALCLOOP
 RTS
*
*
BITMASKS HEX 01020408102040
CHANLINES DFB $10,$1F,$2E,$3D,$4C,$5B,$6A,$79
*
*-------------------------------
* BUFFERS                      *
*-------------------------------
*
TMPBUF DS 38
ROW1BUF DS 40
ROW8BUF DS 40
TRANSBUF DS 40
MSG ASC "READY  RATE:"
RATESTR ASC "000"
 DFB $00
MSGRATE ASC "RATE (00-FF): "
 DFB $00

BUFLO DFB $00
BUFMID DFB $00
BUFHI DFB $00
VALLO DS 1
VALMID DS 1
VALHI DS 1
REMAIN DS 1
MSG2 ASC "TIME: "
TIMESTR ASC "00000000"
 ASC " NS"
 DFB $00
MSGA ASC "A:    "
TIMEASTR ASC "--------"
 ASC " NS"
 DFB $00
MSGB ASC "B:    "
TIMEBSTR ASC "--------"
 ASC " NS"
 DFB $00
*
*-------------------------------
* LINE ADDRESSES               *
*-------------------------------
*
TXTLO DFB $50,$D0,$50,$D0
TXTHI DFB $06,$06,$07,$07
 PUT LINEADDR
*
********************************
********************************
