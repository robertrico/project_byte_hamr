 LST OFF
********************************
* LOGIC ANALYZER DISPLAY       *
********************************
*
 ORG $8000
* ZERO PAGES
PRVBIT EQU $80
CURSMPL EQU $81
TRANS EQU $82
SMPLPTR EQU $83
BUFFPTR EQU $84
CURCOL EQU $90
CURBIT EQU $91
CURMASK EQU $92
TEMP EQU $93
*
* CONSTANTS
NUMCOLS EQU 38 ; COLS PER ROW
BASELINE EQU 0 ; STARTING LINE FOR CHAN 0
 JMP MAIN
 PUT INCLUDE ; INCLUDE DRAW UTILS
*
********************************
* MAIN ROUTINE                 *
********************************
*
MAIN JSR HGR ; CLEAR + HIRES
*
*-------------------------------
*GET SAMPLE MACRO              *
*-------------------------------
*
GETSMPL MAC
 LDA #<]1
 STA SMPLPTR
 LDA #>]1
 STA SMPLPTR+1
 JSR CALCHAN
 <<<
*
*-------------------------------
*DRAW CHANNEL MACRO SETUP      *
*-------------------------------
*
DC MAC
 LDA #<]2
 STA BUFFPTR
 LDA #>]2
 STA BUFFPTR+1
 LDX ]1
 JSR DRCH
 <<<
*
*-------------------------------
* DRAW CHANNELS
*-------------------------------
* CH1
 >>> GETSMPL.SAMPLE1
 >>> DC.#$10;ROW1BUF
 >>> DC.#$11;TRANSBUF
 >>> DC.#$12;TRANSBUF
 >>> DC.#$13;TRANSBUF
 >>> DC.#$14;TRANSBUF
 >>> DC.#$15;TRANSBUF
 >>> DC.#$16;ROW8BUF
* CH2
 >>> GETSMPL.SAMPLE2
 >>> DC.#$1F;ROW1BUF
 >>> DC.#$20;TRANSBUF
 >>> DC.#$21;TRANSBUF
 >>> DC.#$22;TRANSBUF
 >>> DC.#$23;TRANSBUF
 >>> DC.#$24;TRANSBUF
 >>> DC.#$25;ROW8BUF
* CH3
 >>> GETSMPL.SAMPLE3
 >>> DC.#$2E;ROW1BUF
 >>> DC.#$2F;TRANSBUF
 >>> DC.#$30;TRANSBUF
 >>> DC.#$31;TRANSBUF
 >>> DC.#$32;TRANSBUF
 >>> DC.#$33;TRANSBUF
 >>> DC.#$34;ROW8BUF
* CH4
 >>> GETSMPL.SAMPLE4
 >>> DC.#$3D;ROW1BUF
 >>> DC.#$3E;TRANSBUF
 >>> DC.#$3F;TRANSBUF
 >>> DC.#$40;TRANSBUF
 >>> DC.#$41;TRANSBUF
 >>> DC.#$42;TRANSBUF
 >>> DC.#$43;ROW8BUF
* CH5
 >>> GETSMPL.SAMPLE5
 >>> DC.#$4C;ROW1BUF
 >>> DC.#$4D;TRANSBUF
 >>> DC.#$4E;TRANSBUF
 >>> DC.#$4F;TRANSBUF
 >>> DC.#$50;TRANSBUF
 >>> DC.#$51;TRANSBUF
 >>> DC.#$52;ROW8BUF
* CH6
 >>> GETSMPL.SAMPLE6
 >>> DC.#$5B;ROW1BUF
 >>> DC.#$5C;TRANSBUF
 >>> DC.#$5D;TRANSBUF
 >>> DC.#$5E;TRANSBUF
 >>> DC.#$5F;TRANSBUF
 >>> DC.#$60;TRANSBUF
 >>> DC.#$61;ROW8BUF
* CH7
 >>> GETSMPL.SAMPLE7
 >>> DC.#$6A;ROW1BUF
 >>> DC.#$6B;TRANSBUF
 >>> DC.#$6C;TRANSBUF
 >>> DC.#$6D;TRANSBUF
 >>> DC.#$6E;TRANSBUF
 >>> DC.#$6F;TRANSBUF
 >>> DC.#$70;ROW8BUF
* CH8
 >>> GETSMPL.SAMPLE8
 >>> DC.#$79;ROW1BUF
 >>> DC.#$7A;TRANSBUF
 >>> DC.#$7B;TRANSBUF
 >>> DC.#$7C;TRANSBUF
 >>> DC.#$7D;TRANSBUF
 >>> DC.#$7E;TRANSBUF
 >>> DC.#$7F;ROW8BUF
*
*-------------------------------
* DRAW TOP BORDER
* LOAD X BEFORE CALLING
*-------------------------------
*
 LDA #$7F
 LDX #$00
 JSR DRAWBRD
 LDX #$01
 JSR DRAWBRD ; A SHOULD STILL HAVE #$7F

 LDA #$01
 LDX #$02
 JSR DRAWBRD
 LDX #$03
 JSR DRAWBRD ; A SHOULD STILL HAVE #$01
*
*-------------------------------
* DRAW SIDE BORDERS
*-------------------------------
*
 LDX #$02
BORDLR
 LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDA #$03
 LDY #$00
 STA (LOW),Y
 LDA #$60
 LDY #$27
 STA (LOW),Y
 INX
 CPX #$9F
 BNE BORDLR
*
*-------------------------------
* DRAW BOTTOM BORDER
* LOAD X BEFORE CALLING
*-------------------------------
*
 LDA #$7F
 LDX #$9F
 JSR DRAWBRD
 LDX #$9E
 JSR DRAWBRD ; A SHOULD STILL HAVE #$7F

 LDA #$01
 LDX #$9D
 JSR DRAWBRD
 LDX #$9C
 JSR DRAWBRD ; A SHOULD STILL HAVE #$01
*
*-------------------------------
*-------------------------------
* TODO FIX CORNERS
*-------------------------------
*-------------------------------
* END MAIN ROUTINE
*-------------------------------
*
MAINLOOP LDA $C000
 BPL MAINLOOP
 STA $C010 ;CLR
 CMP #$88 ; LEFT?
 BEQ GOLEFT
 CMP #$95 ; RIGHT?
 BEQ GORIGHT
 JMP MAINLOOP ; NEITHER
*
GOLEFT JSR ERASEC
 JSR MOVLEFT
 JSR DRAWCUR
 JMP MAINLOOP
*
GORIGHT JSR ERASEC
 JSR MOVRIGHT
 JSR DRAWCUR
 JMP MAINLOOP
*
********************************
*
*-------------------------------
* SUBROUTINES
*-------------------------------
*
*-------------------------------
* DRAW LOOP                    *
*-------------------------------
*
DRAWBRD PHA  ;STASH THE VAL TO WRITE
 LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY #$00
 PLA  ; RESTORE THE VALUE TO WRITE
]DRAW STA (LOW),Y
 INY
 CPY #NUMCOLS+2 ; TO THE END
 BNE ]DRAW
 RTS
*
*-------------------------------
*DRAW CHANNEL - LOAD X BEFORE  *
*-------------------------------
*
DRCH LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY #$00
]DRAW LDA (BUFFPTR),Y
 STA (LOW),Y
 INY
 CPY #NUMCOLS+1
 BNE ]DRAW
 RTS
*
*-------------------------------
* CALC LOOP                    *
*-------------------------------
*
CALCHAN LDA #$00
 STA PRVBIT ; INIT PREV TO 0
 LDY #$00 ; COL COUNT
CALCLOOP
 LDA (SMPLPTR),Y ; GET SAMPLE
 STA CURSMPL ; SAVE
;CALC TRANSITIONS
;TRANS = SAMPLE EOR ((SAMPLE ASL)
;            AND #$7E ORA PRVBIT)
 ASL ; SHIFT LEFT
 AND #$7E ; CLEAR BITS 0 & 7
 ORA PRVBIT ; BRING IN PREV BIT 6
 EOR CURSMPL ; XOR W/ ORIG = TRANS
 STA TRANS
 STA TRANSBUF,Y ; STORE TRANSITIONS
; CALC ROW1 = SAMPLE ORA TRANS
 LDA CURSMPL
 ORA TRANS
 STA ROW1BUF,Y
; CALC ROW8 = !SAMPLE ORA TRANS
 LDA CURSMPL
 EOR #$7F ;INVERT ONLY 7 BITS
 ORA TRANS
 STA ROW8BUF,Y
; SAVE BIT 6 FOR NEXT COL PRVBIT
 LDA CURSMPL
 AND #$40 ; GET BIT 6
 BEQ SAVEPREV ; IF 0, STORE 0
 LDA #$01 ; IF SET, STORE 1
SAVEPREV STA PRVBIT
 INY
 CPY #NUMCOLS+1
 BNE CALCLOOP
 RTS
*
*-------------------------------
*CURSOR ROUTINES               *
*-------------------------------
*
MOVRIGHT INC CURBIT
 LDA CURBIT
 CMP #$07
 BNE UPDMASKR
 LDA #$00
 STA CURBIT
 INC CURCOL
UPDMASKR LDX CURBIT
 LDA BITMASKS,X
 STA CURMASK
 RTS
*
MOVLEFT DEC CURBIT
 BPL UPDMASKL
 LDA #$06
 STA CURBIT
 DEC CURCOL
UPDMASKL LDX CURBIT
 LDA BITMASKS,X
 STA CURMASK
 RTS
*
DRAWCUR LDX #$00
DCLOOP LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY CURCOL
 LDA (LOW),Y
 ORA CURMASK
 STA (LOW),Y
 INX
 CPX #64
 BNE DCLOOP
 RTS
*
ERASEC LDA CURMASK
 EOR #$7F
 STA TEMP
 LDXA #$00
ECLOOP LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY CURCOL
 LDA (LOW),Y
 AND TEMP
 STA (LOW),Y
 INX
 CPX #64
 BNE ECLOOP
 RTS
*

*
BITMASKS HEX 01020408102040
*

SAMPLE1 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE2 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE3 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE4 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE5 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE6 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE7 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
SAMPLE8 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
 HEX 007F7F00007F7F00007F7F00
*
*
ROW1BUF DW 40
ROW8BUF DW 40
TRANSBUF DW 40
*
*-------------------------------
* LINE ADDRESSES               *
*-------------------------------
 PUT LINEADDR
*
********************************
********************************
