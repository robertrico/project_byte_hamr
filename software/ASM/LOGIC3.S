********************************
* LOGIC ANALYZER DISPLAY       *
********************************
*
 ORG $8000
* ZERO PAGES
PRVBIT EQU $80
CURSMPL EQU $81
TRANS EQU $82
*
* CONSTANTS
NUMCOLS EQU 40 ; COLS PER ROW
BASELINE EQU 0 ; STARTING LINE FOR CHAN 0
 JMP MAIN
 PUT INCLUDE ; INCLUDE DRAW UTILS
*
********************************
* MAIN ROUTINE                 *
********************************
*
MAIN JSR HGR ; CLEAR + HIRES
*
CALCHAN MAC
 LDA #$00
 STA PRVBIT ; INIT PREV TO 0
 LDX #$00 ; COL COUNT
CALCLOOP
 LDA ]1,X ; GET SAMPLE
 STA CURSMPL ; SAVE
* CALC TRANSITIONS
* TRANS = SAMPLE EOR ((SAMPLE ASL) AND #$7E ORA PRVBIT)
 ASL ; SHIFT LEFT
 AND #$7E ; CLEAR BITS 0 & 7
 ORA PRVBIT ; BRING IN PREV BIT 6
 EOR CURSMPL ; XOR W/ ORIG = TRANS
 STA TRANS
 STA TRANSBUF,X ; STORE TRANSITIONS
* CALC ROW1 = SAMPLE ORA TRANS
 LDA CURSMPL
 ORA TRANS
 STA ROW1BUF,X
* CALC ROW8 = !SAMPLE ORA TRANS
 LDA CURSMPL
 EOR #$7F ;INVERT ONLY 7 BITS
 ORA TRANS
 STA ROW8BUF,X
* SAVE BIT 6 FOR NEXT COL PRVBIT
 LDA CURSMPL
 AND #$40 ; GET BIT 6
 BEQ SAVEPREV ; IF 0, STORE 0
 LDA #$01 ; IF SET, STORE 1
SAVEPREV STA PRVBIT
*
 INX
 CPX #NUMCOLS
 BNE CALCLOOP
 <<<
* PASS 2: DRAW LINES
* DRAW LINE MACRO
DRAWCH MAC
 LDX #BASELINE+]1
 LDA HI,X
 STA HIGH
 LDA LO,X
 STA LOW
 LDY #$00
]DRAW LDA ]2
 STA (LOW),Y
 INY
 CPY #NUMCOLS
 BNE ]DRAW
 <<<
 >>> CALCHAN.SAMPLE
 >>> DRAWCH.0;ROW1BUF,Y
 >>> DRAWCH.1;TRANSBUF,Y
 >>> DRAWCH.2;TRANSBUF,Y
 >>> DRAWCH.3;TRANSBUF,Y
 >>> DRAWCH.4;TRANSBUF,Y
 >>> DRAWCH.5;TRANSBUF,Y
 >>> DRAWCH.6;TRANSBUF,Y
 >>> DRAWCH.7;ROW8BUF,Y
*
 >>> CALCHAN.SAMPLE1
 >>> DRAWCH.15;ROW1BUF,Y
 >>> DRAWCH.16;TRANSBUF,Y
 >>> DRAWCH.17;TRANSBUF,Y
 >>> DRAWCH.18;TRANSBUF,Y
 >>> DRAWCH.19;TRANSBUF,Y
 >>> DRAWCH.20;TRANSBUF,Y
 >>> DRAWCH.21;TRANSBUF,Y
 >>> DRAWCH.22;ROW8BUF,Y
*
 >>> CALCHAN.SAMPLE2
 >>> DRAWCH.30;ROW1BUF,Y
 >>> DRAWCH.31;TRANSBUF,Y
 >>> DRAWCH.32;TRANSBUF,Y
 >>> DRAWCH.33;TRANSBUF,Y
 >>> DRAWCH.34;TRANSBUF,Y
 >>> DRAWCH.35;TRANSBUF,Y
 >>> DRAWCH.36;TRANSBUF,Y
 >>> DRAWCH.37;ROW8BUF,Y
DONE JMP $FF69 ; INF LOOP
*
*
SAMPLE HEX 7F007F007F007F00
 HEX 000000007F7F7F7F
 HEX 007F007F007F007F
 HEX 7F7F7F7F00000000
 HEX 0F0F0F0F0F0F0F0F
SAMPLE1 HEX 7F007F007F007F00
 HEX 007F007F007F007F
 HEX 7F7F7F7F00000000
 HEX 000000007F7F7F7F
 HEX 0F0F0F0F0F0F0F0F
SAMPLE2 HEX 7F007F007F007F00
 HEX 000000007F7F7F7F
 HEX 0F0F0F0F0F0F0F0F
 HEX 007F007F007F007F
 HEX 7F7F7F7F00000000
* CALC BUFFERS (40 BYTES)
ROW1BUF DS 40
TRANSBUF DS 40
ROW8BUF DS 40
*
 PUT LINEADDR ; INCLUDE LINE ADDRESS LUT
